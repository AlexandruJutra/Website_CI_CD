resource "random_integer" "bucket_suffix" {
  min = 1000
  max = 9999
}

resource "aws_s3_bucket" "Dev_bucket" {
  bucket        = "dev-bucket-${var.bucket_prefix}-${random_integer.bucket_suffix.result}"
  force_destroy = true

  tags = {
    Name        = "My bucket"
    Environment = "Dev"
  }

  lifecycle {
    create_before_destroy = true
  }
}

# resource "aws_s3_bucket" "Test_bucket" {
#   bucket        = "test-bucket-${var.bucket_prefix}-${random_integer.bucket_suffix.result}"
#   force_destroy = true

#   tags = {
#     Name        = "My bucket"
#     Environment = "Test"
#   }
# }

# resource "aws_s3_bucket" "prod_bucket" {
#   bucket        = "prod-bucket-${var.bucket_prefix}-${random_integer.bucket_suffix.result}"
#   force_destroy = true

#   tags = {
#     Name        = "My bucket"
#     Environment = "Prod"
#   }
# }

resource "aws_s3_bucket_public_access_block" "block" {
  bucket = aws_s3_bucket.Dev_bucket.id

  block_public_acls       = true
  block_public_policy     = true
  ignore_public_acls      = true
  restrict_public_buckets = true
}

resource "aws_cloudfront_origin_access_control" "oac" {
  name                              = "demo_oac"
  description                       = "Example Policy"
  origin_access_control_origin_type = "s3"
  signing_behavior                  = "always"
  signing_protocol                  = "sigv4"
}

resource "aws_s3_bucket_policy" "allow_access_from_another_account" {
  bucket = aws_s3_bucket.Dev_bucket.id
  depends_on = [ aws_s3_bucket_public_access_block.block ]
  policy = jsonencode({
  "Version": "2012-10-17",
  "Statement": [
    {
      "Sid": "AllowCloudFront",
      "Effect": "Allow",
      "Principal": {
        "Service": "cloudfront.amazonaws.com"
      },
      "Action": [
        "s3:GetObject"
      ],
      "Resource": "${aws_s3_bucket.Dev_bucket.arn}/*",
      "Condition": {
        "StringEquals": {
          "AWS:SourceArn": aws_cloudfront_distribution.s3_distribution.arn
        }
      }
    }
  ]
})
}

resource "aws_s3_object" "object" {
  for_each = fileset("${path.module}/../s3_bucket", "**/*")
  bucket = aws_s3_bucket.Dev_bucket.id
  key    = each.value
  source = "${path.module}/../s3_bucket/${each.value}"
  etag = filemd5("${path.module}/../s3_bucket/${each.value}")
  content_type = lookup({
    "html" = "text/html",
    "css"  = "text/css",
    "js"   = "application/javascript",
    "png"  = "image/png",
    "jpg"  = "image/jpeg",
  }, split(".", each.value)[length(split(".", each.value)) - 1], "application/octet-stream")
}

resource "aws_cloudfront_distribution" "s3_distribution" {
  origin {
    domain_name              = aws_s3_bucket.Dev_bucket.bucket_regional_domain_name
    origin_access_control_id = aws_cloudfront_origin_access_control.oac.id
    origin_id                = local.s3_origin_id
  }

  enabled             = true
  is_ipv6_enabled     = true
  comment             = "Some comment"
  default_root_object = "index.html"

  default_cache_behavior {
    allowed_methods  = ["GET", "HEAD"]
    cached_methods   = ["GET", "HEAD"]
    target_origin_id = local.s3_origin_id

    forwarded_values {
      query_string = false

      cookies {
        forward = "none"
      }
    }

    viewer_protocol_policy = "redirect-to-https"
    min_ttl                = 0
    default_ttl            = 0
    max_ttl                = 0
  }

  price_class = "PriceClass_200"

  restrictions {
    geo_restriction {
      restriction_type = "none"
    }
  }

  viewer_certificate {
    cloudfront_default_certificate = true
  }
}



